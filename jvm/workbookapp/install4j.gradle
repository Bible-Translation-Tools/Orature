// Need buildscript/apply plugin because plugins {} only works in build.gradle scripts
buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "gradle.plugin.install4j.install4j:gradle_plugin:$install4jPluginVer"
    }
}

apply plugin: "com.install4j.gradle"

//install4j tasks for building win/nix/mac installers. Executable jar can be built without it
install4j {
    //pass in the install4j directory via -P if it's not in the default install location, e.q. for use with travis or arbitrary paths
    if (project.hasProperty('gradlewInstall4jDirectory')) {
        installDir = file(gradlewInstall4jDirectory)
    }
    //license must be passed via gradlew commandline -P as it can't be commit to public source control
    if (project.hasProperty('gradlewCommandLicenseProp')) {
        license = gradlewCommandLicenseProp
    }
}

task install4jdeploy(type: com.install4j.gradle.Install4jTask) {
    projectFile = 'otter.install4j'
    //Pass Travis Build ID to install4j for versioning
    if (project.hasProperty('gradlewCommandVersionProp')) {
        release = gradlewCommandVersionProp
    }
}

install4jdeploy.dependsOn build