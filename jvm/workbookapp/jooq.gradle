import groovy.sql.Sql

ext {
    sqlDirPath = [projectDir.path, "src", "main", "resources", "sql", "CreateAppDb.sql"].join(File.separator)
    sqliteFile = [projectDir.path, "test.sqlite"].join(File.separator)
    sqliteConnect = "jdbc:sqlite:${ext.sqliteFile}"
}

configurations {
    sqllite
}

dependencies {
    // Jooq
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: "$sqliteJdbcVer"
    implementation "org.jooq:jooq:$jooqVer"
    jooqGenerator group: 'org.jooq', name: 'jooq-meta-extensions', version: "$jooqVer"
    jooqGenerator "org.xerial:sqlite-jdbc:$sqliteJdbcVer"
    sqllite "org.xerial:sqlite-jdbc:$sqliteJdbcVer"
}

task createDb {
    //Add the full path to the sqlite-jdbc jar to the list of URLs
    //that Gradle will search for classes and resources
    //The code block below allows Gradle to find and use the executable jar for sqlite-jdbc
    //using its full path on the local machine
    URLClassLoader loader = GroovyObject.class.classLoader
    configurations.sqllite.each { File file ->
        loader.addURL(file.toURL())
    }
    String[] text = new File(sqlDirPath).text.split(';')
    text.collect { it.trim() }.findAll { !it.isEmpty() && !it.startsWith("--") }.each {
        def sql = Sql.newInstance(sqliteConnect, "org.sqlite.JDBC")
        sql.execute(it)
        if(!(System.getProperty("os.name")).toUpperCase().contains("MAC")) {
            sql.close()
        }
    }
}

jooq {
    version = "$jooqVer"

    configurations {
        main {  // name of the jOOQ configuration
            generateSchemaSourceOnCompilation = true  // default (can be omitted)

            generationTool {
                jdbc {
                    driver = 'org.sqlite.JDBC'
                    url = sqliteConnect
                }
                generator {
                    name = "org.jooq.codegen.DefaultGenerator"
                    database {
                        name = 'org.jooq.meta.sqlite.SQLiteDatabase'
                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'jooq'
                        directory = 'src/main/java/jooq'
                    }
                    strategy.name = "org.jooq.codegen.DefaultGeneratorStrategy"
                }
            }
        }
    }
}

generateJooq.doFirst {
    project.file('src/main/java/jooq').deleteDir()
}

generateJooq.doLast {
    project.file(sqliteFile).delete()
}

generateJooq.dependsOn createDb
compileKotlin.dependsOn generateJooq