buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.9.1'
    }
}

apply plugin: 'com.novoda.bintray-release'

group 'org.wycliffeassociates.otter'
version '0.0.1'

repositories {
    ivy {
        url 'https://git.door43.org'
        patternLayout {
            artifact '[organisation]/[module]/archive/[revision].[ext]'
        }
        metadataSources { artifact() }
    }
    ivy {
        url 'http://td.unfoldingword.org/exports/'
        patternLayout {
            artifact '[artifact].[ext]'
        }
        metadataSources { artifact() }
    }
}

configurations {
    content.extendsFrom runtime
}

dependencies {
    content 'wycliffeassociates:en_ulb:v19-10@zip'
    content 'unfoldingword:langnames@json'
    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlinVer"
    implementation project(":common")

    implementation "org.slf4j:slf4j-api:$slf4jApiVer"
    implementation "com.google.dagger:dagger:$daggerVer"
}

task copyToResources(type: Copy) {
    dependsOn configurations.content
    from configurations.content
    into 'src/main/resources/content'
    rename {
        String filename ->
            if (filename.matches(".*.json\$")) {
                filename.replaceAll('-\\.json$', '.json')
            } else {
                filename.replaceAll('-v\\d{2}-\\d{2}\\.zip$', '.zip')
            }
    }
}

compileJava.dependsOn copyToResources

publish {
    def groupProjectID = 'org.wycliffeassociates.otter'
    def artifactProjectID = 'assets'
    def publishVersionID = "0.0.1"

    userOrg = 'bible-translation-tools'
    repoName = 'otter'
    groupId = groupProjectID
    artifactId = artifactProjectID
    publishVersion = publishVersionID
    desc = ''
    website = 'https://github.com/Bible-Translation-Tools/otter'
}
