buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.9.1'
    }
}

apply plugin: 'com.novoda.bintray-release'

group 'org.wycliffeassociates.otter'
version '0.0.1'

repositories {
    ivy {
        url 'https://git.door43.org'
        patternLayout {
            artifact '[organisation]/[module]/archive/[revision].[ext]'
        }
    }
    ivy {
        url 'http://td.unfoldingword.org/exports/'
        patternLayout {
            artifact '[artifact].[ext]'
        }
    }
}

configurations {
    content
}

dependencies {
    content 'wycliffeassociates:en_ulb:v19-10@zip'
    content 'unfoldingword:langnames@json'
}

task copyToResources(type: Copy) {
    from configurations.content
    into 'src/main/resources/content'
    rename {
        String filename ->
            if (filename.contains('.json')) {
                filename.replaceAll('-.*\\.json', '.json')
            } else {
                filename.replaceAll('-.*\\.zip', '.zip')
            }
    }
}

build.finalizedBy(copyToResources)

publish {
    def groupProjectID = 'org.wycliffeassociates.otter'
    def artifactProjectID = 'assets'
    def publishVersionID = "0.0.1"

    userOrg = 'bible-translation-tools'
    repoName = 'otter'
    groupId = groupProjectID
    artifactId = artifactProjectID
    publishVersion = publishVersionID
    desc = ''
    website = 'https://github.com/Bible-Translation-Tools/otter'
}