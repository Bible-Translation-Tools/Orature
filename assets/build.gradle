group 'org.wycliffeassociates.otter'
version '0.0.1'

repositories {
    ivy {
        url 'https://git.door43.org'
        patternLayout {
            artifact '[organisation]/[module]/archive/[revision].[ext]'
        }
        metadataSources { artifact() }
    }
    ivy {
        url 'https://td.unfoldingword.org/exports/'
        patternLayout {
            artifact '[artifact].[ext]'
        }
        metadataSources { artifact() }
    }
    ivy {
        url 'https://nightlybuilds.s3.amazonaws.com/Bible-Translation-Tools/artwork/'
        patternLayout {
            artifact '[artifact].[ext]'
        }
        metadataSources { artifact() }
    }
    ivy {
        url 'https://nexus-registry.walink.org/repository/maven-releases/'
        patternLayout {
            artifact '[organisation]/[module]/[revision]/[artifact]-[revision].[ext]'
        }
        metadataSources { artifact() }
    }
}

configurations {
    content.extendsFrom runtime
}

dependencies {
    content 'bible-translation-tools:en_art_wa:1.0.0@zip'
    content 'bible-translation-tools:en_art_sp:1.0.0@zip'
    content 'wycliffeassociates:en_ulb:v19-10@zip'
    content 'unfoldingword:langnames@json'
    content 'bible-translation-tools:bible_artwork@zip'
    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlinVer"
    implementation project(":common")

    implementation "org.slf4j:slf4j-api:$slf4jApiVer"
    implementation "com.google.dagger:dagger:$daggerVer"
}

task copyToResources(type: Copy) {
    dependsOn configurations.content
    from configurations.content
    into 'src/main/resources/content'
    rename {
        String filename ->
            if (filename.matches(".*.json\$")) {
                filename.replaceAll('-\\.json$', '.json')
            } else {
                filename.replaceAll('-.*.zip$', '.zip')
            }
    }
}

compileJava.dependsOn copyToResources
